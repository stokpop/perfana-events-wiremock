import org.gradle.api.plugins.internal.DefaultJavaPluginExtension
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'java-library'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    id 'maven-publish'
    id 'signing'
    // check dependency updates: ./gradlew dependencyUpdates -Drevision=release
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'io.codearte.nexus-staging' version '0.21.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation 'nl.stokpop:event-scheduler:2.0.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.10'
    implementation 'org.apache.httpcomponents:httpmime:4.5.10'
    testImplementation 'junit:junit:4.13-rc-1'
    testImplementation 'com.github.tomakehurst:wiremock:2.25.1'
}

group = 'nl.stokpop'
version = '1.1.0'
description = 'test-events-wiremock'
sourceCompatibility = '1.8'

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

license {
    // license check is broken on windows, skip
    if (OperatingSystem.current().isWindows()) ignoreFailures = true
    header = file("src/main/resources/META-INF/LICENSE.txt")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Peter Paul Bakker, Stokpop Software Solutions'
    exclude "**/*.txt"
    // the standard java style makes intellij complain about dangling javadoc
    mapping('java', 'SLASHSTAR_STYLE')
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar) {
            archiveClassifier.set('javadoc')
            from javadoc
        }

        task sourcesJar(type: Jar) {
            archiveClassifier.set('sources')
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }
        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    String repositoryUrl
                    if (version.endsWith("SNAPSHOT")) {
                        repositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                    }
                    else {
                        repositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    }
                    repository(url: repositoryUrl) {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.getName()
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/stokpop/test-events-wiremock.git'

                        scm {
                            connection 'scm:git:https://github.com/stokpop/test-events-wiremock.git'
                            developerConnection 'scm:git:git@github.com:stokpop/test-events-wiremock.git'
                            url 'https://github.com/stokpop/test-events-wiremock.git'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'stokpop'
                                name = 'Peter Paul Bakker'
                                email = 'peter.paul.bakker@stokpop.nl'
                            }
                        }
                    }
                }
            }
        }

    }
}

nexusStaging {
    username = project.properties['ossrhUser'] ?: "noUser"
    password = project.properties['ossrhPassword'] ?: "noPassword"
}

